dnl
dnl Process this file with autoconf to produce a configure script.
dnl
dnl

AC_DEFUN([AC_CXX_PARTIAL_SPECIALIZATION],
[AC_CACHE_CHECK(whether the compiler supports partial specialization,
ac_cv_cxx_partial_specialization,
[AC_LANG_SAVE
 AC_LANG_CPLUSPLUS
 AC_TRY_COMPILE([
template<class T, int N> class A            { public : enum e { z = 0 }; };
template<int N>          class A<double, N> { public : enum e { z = 1 }; };
template<class T>        class A<T, 2>      { public : enum e { z = 2 }; };
],[return (A<int,3>::z == 0) && (A<double,3>::z == 1) && (A<float,2>::z == 2);],
 ac_cv_cxx_partial_specialization=yes, ac_cv_cxx_partial_specialization=no)
 AC_LANG_RESTORE
])
if test "$ac_cv_cxx_partial_specialization" = yes; then
  AC_DEFINE(HAVE_PARTIAL_SPECIALIZATION,,
            [define if the compiler supports partial specialization])
fi
])

AC_INIT(Makefile.am)

AM_INIT_AUTOMAKE(EasySoap++, 0.5)
AM_CONFIG_HEADER(include/es_config.h)
AC_CANONICAL_HOST

AC_CYGWIN

dnl Checks for programs.
AC_PROG_CXX
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL
AC_PATH_PROG(MAKEDEPEND, makedepend, $TRUE)

dnl Check C++ compiler characteristics
AC_CXX_PARTIAL_SPECIALIZATION

dnl Checks for libraries.
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(socket, send)
AC_CHECK_LIB(wsock32, WSAStartup)
AC_CHECK_LIB(crypto, ERR_put_error)
AC_CHECK_LIB(ssl, SSL_connect)
AC_CHECK_LIB(expat, XML_Parse)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADER(wchar.h,
	AC_DEFINE(HAVE_WCHAR_H, 1, "Define if you have the <wchar.h> header file."),
	AC_EGREP_HEADER(wchar_t, stddef.h, 
		AC_DEFINE(STDDEF_HAS_WCHART, 1, "Define if wchar_t is defined in the <stddef.h> header file.")))

AC_CHECK_HEADERS( \
	errno.h \
	fcntl.h \
	ieeefp.h \
	malloc.h \
	math.h \
	memory.h \
	netdb.h \
	resolv.h \
	stdlib.h \
	string.h \
	unistd.h \
	arpa/inet.h \
	arpa/nameser.h \
	netinet/in.h \
	netinet/tcp.h \
	sys/filio.h \
	sys/ioctl.h \
	sys/socket.h \
	sys/types.h \
	sys/unistd.h \
)


dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS( \
	mktime \
	select \
	socket \
	strdup \
	strtol \
	snprintf \
	strerror \
)

AC_OUTPUT(Makefile simpleserver/Makefile validator/Makefile tests/Makefile src/Makefile src/abyss/Makefile src/abyss/src/Makefile include/Makefile)

